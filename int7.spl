alias physicalSP S0;
physicalSP = ([PTBR + 2*(SP/512)]*512) + (SP%512);

alias sysCallNum S1;
sysCallNum = [physicalSP - 1];

alias currentPID S2;
currentPID=(PTBR - 1024)/8;

alias currentPCB S3;
currentPCB=READY_LIST + 32*currentPID;


if (sysCallNum == 10) then

	alias newPID S4;
	newPID = 0;
	alias newPCB S5;
	while(newPID < 32) do
		newPCB = READY_LIST + 32*newPID;
		if ([newPCB + 1] == (100 + currentPID)) then
			[newPCB + 1]=1;
		endif;
		newPID = newPID + 1;
	endwhile;

	

	currentPID=0;
	while(currentPID < 32) do
		currentPCB=READY_LIST + 32*currentPID;
		if ([currentPCB + 1] == 1) then
			break;
		endif;
		currentPID=currentPID+1;
	endwhile;

	if (currentPID == 32) then
		halt;
	endif;

	currentPID=(PTBR - 1024)/8;
	currentPCB=READY_LIST + 32*currentPID;

	alias counter S6;
	alias FileIndex S7;
	counter=0;
	while(counter < 8) do
		FileIndex=[currentPCB + 2*counter + 15];

		if (FileIndex == -1) then
			counter=counter+1;
			continue;
		endif;

		[FILE_TABLE + 2*FileIndex + 1]=[FILE_TABLE + 2*FileIndex + 1]-1;
		if ([FILE_TABLE + 2*FileIndex + 1] == 0) then
			[FILE_TABLE + 2*FileIndex]=-1;
		endif;

		[currentPCB + 2*counter + 15]=-1;
		[currentPCB + 2*counter + 16]=-1;
		counter=counter+1;
	endwhile;

	[currentPCB + 1]=0;

	
	alias DataBlock S9;
	counter=0;
	while(counter < 4) do
		DataBlock=[PTBR + 2*counter];
		if (([PTBR + 2*counter + 1] == "01") || ([PTBR + 2*counter + 1] == "11")) then
			[MEM_LIST + DataBlock]=[MEM_LIST + DataBlock]-1;
		else
			if (DataBlock >= 448) then
				[DISK_LIST + DataBlock]=[DISK_LIST + DataBlock]-1;
				store(6,20);
			endif;
		endif;
		[PTBR + 2*counter]=-1;
		[PTBR + 2*counter + 1]="00";
		counter=counter+1;
	endwhile;
endif;

if(sysCallNum == 13) then //wait
	alias waitingPID S4;
	waitingPID = [physicalSP - 3];
	alias waitingPCB S5;
	waitingPCB = READY_LIST + 32*waitingPID;
	alias i S6;
	if([waitingPCB + 1] == 0 || [waitingPCB + 1] == 2) then
		[physicalSP - 2] = -1;  //if process is running or terminated return -1;
		ireturn;
	endif; 

	[currentPCB + 1] = 100 + waitingPID;
	[currentPCB + 2] = BP;
	[currentPCB + 3] = SP - 1;
	[currentPCB + 4] = [physicalSP];
	[currentPCB + 5] = PTBR;
	[currentPCB + 6] = PTLR;
	[currentPCB + 7] = R0;
	[currentPCB + 8] = R1;
	[currentPCB + 9] = R2;
	[currentPCB + 10] = R3;
	[currentPCB + 11] = R4;
	[currentPCB + 12] = R5;
	[currentPCB + 13] = R6;
	[currentPCB + 14] = R7;
	[physicalSP - 2] = 0;
endif;

if(sysCallNum == 14) then //signal
	alias i S4;
	i = 0;
	while(i < 32) do
		if([READY_LIST + 32*i + 1] == 100 + currentPID) then
			[READY_LIST + 32*i + 1] = 1;
		endif;
		i = i + 1;
	endwhile;
	[physicalSP - 2] = 0;
endif;

if(sysCallNum == 10 || sysCallNum == 13) then
	alias i S7;
	i = currentPID + 1;
	alias newPID S8;
	alias newPCB S9;

	while(1) do
		if (i == 32) then
			i=0;
		endif;
		if ([READY_LIST + 32*i + 1] == 1) then
			break;
		endif;
		i = i + 1;
	endwhile;

	newPID = i;
	newPCB = READY_LIST + 32*i;
//setting reg vals
	BP = [newPCB + 2];
	SP = [newPCB + 3];

	PTBR = [newPCB + 5];
	PTLR = [newPCB + 6];

	R0 = [newPCB + 7];
	R1 = [newPCB + 8];
	R2 = [newPCB + 9];
	R3 = [newPCB + 10];
	R4 = [newPCB + 11];
	R5 = [newPCB + 12];
	R6 = [newPCB + 13];
	R7 = [newPCB + 14];

	SP = SP + 1; 
	alias newphysicalSP S10; 
	newphysicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512); 
	[ newphysicalSP ] = [ newPCB + 4 ]; //setting IP

	[newPCB + 1] = 2;
endif;

ireturn;